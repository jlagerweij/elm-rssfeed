plugins {
  id "com.moowork.node" version "1.1.0"
}

node {
  version = '6.9.5'
  npmVersion = '3.10.10'
  download = true
}

task elmPackageInstall(type: Exec, dependsOn: npmInstall) {
  commandLine "node_modules/.bin/elm-package install -y".split(' ')
  inputs.file file('elm-package.json')
  outputs.dir file('elm-stuff')
}

task webpack(type: NpmTask, dependsOn: elmPackageInstall) {
  args = ['run', 'build']
}

task webpackDevServer(type: NodeTask, dependsOn: elmPackageInstall) {
  script = file('node_modules/webpack-dev-server/bin/webpack-dev-server')
  args ["--hot"]
}

task buildDeploy(type: Copy, dependsOn: webpack) {
  from ('build/web/static') {
    into 'static'
  }
  from 'build/web/index.html'
  into 'build/deploy'
}

task deployStatic(type: Exec, dependsOn: buildDeploy) {
  commandLine 'scp -r build/deploy/static elmo:/hdd/web/j/rss/elm/'.split(" ")
}
task deployHtml(type: Exec, dependsOn: buildDeploy) {
  commandLine 'scp -r build/deploy/index.html elmo:/hdd/web/j/rss/elm/'.split(" ")
}
task deploy(dependsOn: [deployStatic, deployHtml]) {

}
